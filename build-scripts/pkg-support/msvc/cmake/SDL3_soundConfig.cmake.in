# @<@PROJECT_NAME@>@ CMake configuration file:
# This file is meant to be placed in a cmake subfolder of  @<@PROJECT_NAME@>@-devel-@<@PROJECT_VERSION@>@-VC.zip

include(FeatureSummary)
set_package_properties(SDL3_sound PROPERTIES
    URL "https://www.libsdl.org/icculus/SDL_sound/"
    DESCRIPTION "SDL_sound is a library that handles the decoding of several popular sound file formats."
)

cmake_minimum_required(VERSION 3.0...4.0)

# Copied from `configure_package_config_file`
macro(check_required_components _NAME)
    foreach(comp ${${_NAME}_FIND_COMPONENTS})
        if(NOT ${_NAME}_${comp}_FOUND)
            if(${_NAME}_FIND_REQUIRED_${comp})
                set(${_NAME}_FOUND FALSE)
            endif()
        endif()
    endforeach()
endmacro()

if(SDL_CPU_X86)
    set(_sdl_arch_subdir "x86")
elseif(SDL_CPU_X64 OR SDL_CPU_ARM64EC)
    set(_sdl_arch_subdir "x64")
elseif(SDL_CPU_ARM64)
    set(_sdl_arch_subdir "arm64")
else()
    set(SDL3_image_FOUND FALSE)
    return()
endif()

set(_sdl3sound_incdir       "${CMAKE_CURRENT_LIST_DIR}/../include")
set(_sdl3sound_library      "${CMAKE_CURRENT_LIST_DIR}/../lib/${_sdl_arch_subdir}/SDL3_sound.lib")
set(_sdl3sound_dll          "${CMAKE_CURRENT_LIST_DIR}/../lib/${_sdl_arch_subdir}/SDL3_sound.dll")

# All targets are created, even when some might not be requested though COMPONENTS.
# This is done for compatibility with CMake generated SDL3_sound-target.cmake files.

set(SDL3_sound_SDL3_sound-shared_FOUND TRUE)
if(NOT TARGET SDL3_sound::SDL3_sound-shared)
    add_library(SDL3_sound::SDL3_sound-shared SHARED IMPORTED)
    set_target_properties(SDL3_sound::SDL3_sound-shared
        PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES "${_sdl3sound_incdir}"
            IMPORTED_IMPLIB "${_sdl3sound_library}"
            IMPORTED_LOCATION "${_sdl3sound_dll}"
            COMPATIBLE_INTERFACE_BOOL "SDL3_SHARED"
            INTERFACE_SDL3_SHARED "ON"
    )
endif()

unset(_sdl_arch_subdir)
unset(_sdl3sound_incdir)
unset(_sdl3sound_library)
unset(_sdl3sound_dll)

set(SDL3_sound_SDL3_sound-static_FOUND FALSE)

set(SDL3_sound_SDL3_sound_FOUND FALSE)
if(SDL3_sound_SDL3_sound-hared_FOUND OR SDL3_sound_SDL3_sound-static_FOUND)
    set(SDL3_sound_SDL3_sound_FOUND TRUE)
endif()

function(_sdl_create_target_alias_compat NEW_TARGET TARGET)
    if(CMAKE_VERSION VERSION_LESS "3.18")
        # Aliasing local targets is not supported on CMake < 3.18, so make it global.
        add_library(${NEW_TARGET} INTERFACE IMPORTED)
        set_target_properties(${NEW_TARGET} PROPERTIES INTERFACE_LINK_LIBRARIES "${TARGET}")
    else()
        add_library(${NEW_TARGET} ALIAS ${TARGET})
    endif()
endfunction()

# Make sure SDL3_sound::SDL3_sound always exists
if(NOT TARGET SDL3_sound::SDL3_sound)
    if(TARGET SDL3_sound::SDL3_sound-shared)
        _sdl_create_target_alias_compat(SDL3_sound::SDL3_sound SDL3_sound::SDL3_sound-shared)
    endif()
endif()

check_required_components(SDL3_sound)
